bindkey -e

has 'history-substring-search-up' &&
    bindkey '^P' history-substring-search-up
has 'history-substring-search-down' &&
    bindkey '^N' history-substring-search-down

# Insert a last word
zle -N insert-last-word smart-insert-last-word
zstyle :insert-last-word match '*([^[:space:]][[:alpha:]/\\]|[[:alpha:]/\\][^[:space:]])*'
bindkey '^]' insert-last-word

# Surround a forward word by single quote
quote-previous-word-in-single() {
    modify-current-argument '${(qq)${(Q)ARG}}'
    zle vi-forward-blank-word
}
zle -N quote-previous-word-in-single
bindkey '^Q' quote-previous-word-in-single

# Surround a forward word by double quote
quote-previous-word-in-double() {
    modify-current-argument '${(qqq)${(Q)ARG}}'
    zle vi-forward-blank-word
}
zle -N quote-previous-word-in-double
bindkey '^Xq' quote-previous-word-in-double

# C-r
_fzf-select-history() {
    BUFFER=$(
        history \
            | sort -k1,1nr \
            | perl -ne 'BEGIN { my @lines = (); } s/^\s*\d+\s*//; $in=$_; if (!(grep {$in eq $_} @lines)) { push(@lines, $in); print $in; }' \
            | fzf --query "$LBUFFER"
          )

    CURSOR=$#BUFFER
    zle reset-prompt
}
zle -N _fzf-select-history
bindkey '^r' _fzf-select-history

_start-tmux-if-it-is-not-already-started() {
    BUFFER="${${${(M)${+commands[tmuxx]}#1}:+tmuxx}:-tmux}"
    if has "tmux_automatically_attach"; then
        BUFFER="tmux_automatically_attach"
    fi
    CURSOR=$#BUFFER
    zle accept-line
}
zle -N _start-tmux-if-it-is-not-already-started
if ! [[ -n $TMUX ]]; then
    bindkey '^T' _start-tmux-if-it-is-not-already-started
fi
